FUNCTION_BLOCK fight	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	health : Real;
	numberOfWeapons : Real;
END_VAR

VAR_OUTPUT				// Define output variable
	damage : Real;
END_VAR

FUZZIFY health		
	TERM poor := (0, 1) (10, 0);
	TERM good := (10, 0) (25,1) (40,1) (60,0);
	TERM excellent := (60, 0) (100, 1);
END_FUZZIFY

FUZZIFY numberOfWeapons
	TERM poorlyArmed := (0, 1) (1, 1) (3,0) ;
	TERM wellArmed := (5,0) (10,1);
END_FUZZIFY

DEFUZZIFY damage
	TERM light := (0,0) (5,1) (10,0);
	TERM heavy := (10,0) (20,1) (30,0);
	TERM severe := (30,0) (40,1) (50,0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF health IS poor AND numberOfWeapons IS poorlyArmed THEN damage IS severe;
	RULE 2 : IF health IS good THEN damage IS heavy; 
	RULE 3 : IF health IS excellent AND numberOfWeapons IS wellArmed THEN damage IS light;
	RULE 4 : IF health IS excellent AND numberOfWeapons IS poorlyArmed THEN damage IS heavy;
END_RULEBLOCK

END_FUNCTION_BLOCK

